//===== Cronus Script ========================================
//= Hipnotizadora
//===== Por: =================================================
//= L0ne_W0lf
//===== Versão Atual: ========================================
//= 1.0b
//===== Comentários Adicionais: ==============================
//= 1.0 First version. [L0ne_W0lf]
//= 1.0a Traduzido. [SoulBlaker]
//= 1.0b Revisado. [RoM]
//============================================================

prontera,146,232,4	script	Hipnotizadora#2	124,{
	show "[Hipnotizadora]";
	show "Olá, aventureiro!";
	show "Fui mandada pela Academia de Hipnotismo";
	show "para ajudar os iniciantes que se arrependem de não terem aprendido suas habilidades do jeito que gostariam.";
	next;
	show "[Hipnotizadora]";
	show "Se seu Nível de Base não for maior que 40, e se você estiver numa Classe 1-1, como:";
	show "Espadachim, Noviço, Mago, Gatuno, Arqueiro, Mercador, Taekwon, Ninja ou Justiceiro.";
	show "Então você pode usufruir de meu Serviço de Redistribuição de Pontos de Habilidades.";
	next;
	show "[Hipnotizadora]";
	show "Desde que você atenda a essas condições, poderei prestar meu Serviço de Redistribuição de Pontos de Habilidade";
	show "sem qualquer limitação. Agora, que tal isso?";
	next;
	switch(select("Redistribuição de Pontos de Habilidade?:^FF0000Redistribuir os Pontos de Habilidade^000000:Cancelar")) {

	case 1:
		show "[Hipnotizadora]";
		show "A Redistribuição de Pontos de Habilidade dá outra chance aos aventureiros de distribuir seus Pontos.";
		show "Se não estiver satisfeito com suas habilidades atuais, então você pode usufruir meu serviço.";
		next;
		show "[Hipnotizadora]";
		show "Lembre-se que só é possível redistribuir seus pontos de Habilidade se não estiver carregando nenhum item com você.";
		show "Por favor, antes use o serviço de Armazenagem Kafra para reduzir seu peso.";
		next;
		show "[Hipnotizadora]";
		show "Ah, e se por acaso você estiver equipado com um ^FF0000Carrinho^000000, livre-se deles antes.";
		show "Esse item costuma interferir na hipnose por alguma razão estranha e inexplicável.";
		close;

	case 2:
		show "[Hipnotizadora]";
		show "Então, você já decidiu que quer ^FF0000Redistribuir os Pontos de Habilidade^000000.";
		show "Podemos continuar?";
		next;
		if (select("Sim:Não") == 1) {
			if (BaseLevel > 40) {
				show "[Hipnotizadora]";
				show "Lamento, mas personagens com o Nível de Base maior que 40 são inelegíveis para";
				show "que eu possa redistribuir os Pontos de Habilidades.";
				close;
			}
			if ((Class > 0 && Class < 7 && Upper == 0) || (Class == Job_Gunslinger) || (Class == Job_Ninja) || (Class == Job_Taekwon)) {
				if (checkcart()) {
					show "[Hipnotizadora]";
					show "Oh! Remova o seu carrinho antes de restribuir os seus pontos de habilidades.";
					show "Obrigado por colaborar!";
					close;
				}
				if (Weight) {
					show "[Hipnotizadora]";
					show "Ah, você poderia reduzir o peso que está carregando em seu iventário para ^FF00000^00000?";
					show "Desculpe, mas não vou poder redistribuir seus pontos de Habilidade de outra forma...";
					close;
				}
				show "[Hipnotizadora]";
				show "Obrigado por você usar os meus serviços de redistribuição de Pontos de Habilidades.";
				show "Oh, e muita sorte para você em suas viagens, aventureiro.";
				ResetSkill;
				close;
			}
			show "[Hipnotizadora]";
			show "Desculpe, mas sua Classe não está apta para o Serviço de Redistribuição de Pontos de Habilidade.";
			show "Posso redistribuir os pontos apenas para:";
			show "Espadachins, Noviços, Magos, Gatunos, Arqueiros, Mercadores, Taekwon, Ninjas ou Justiceiros.";
			show "Ou seja, Classes 1-1.";
			close;
		}
		show "[Hipnotizadora]";
		show "Muito obrigado, e boa sorte em suas aventuras.";
		show "Bem, então tome cuidado. Não seria nada bom se machucar por aí.";
		close;

	case 3:
		show "[Hipnotizadora]";
		show "Hmm...? Saindo para suas aventuras?";
		show "Bem, então tome cuidado. Não seria nada bom se machucar por aí.";
		close;
	}

}
