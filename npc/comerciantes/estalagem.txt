//===== Cronus Script =====================================
//= Estalagem
//===== Por: ==============================================
//= SoulBlaker
//===== Versão Atual: =====================================
//= Darkchild (1.1)
//= Playtester (1.2)
//===== Versão Atual: ===================================== 
//= 2.8
//===== Descrição: ========================================
//= Estalagem, Salvar e Curar.
//============================================================
//= variáveis:
//= arg0 - name of npc
//= arg1 - name of the inn
//= arg2 - map to save at
//= arg3, arg4 - x and y cord. to save at
//= @cost - cost of renting a room
//===== Cometários adicionais: ============================
//= 1.1 Blind Effect By Kobra_k88 (Taken from his old script)
//= I added it to all of them though [Darkchild]
//= 1.1a Minor bug fixes and optimizations. Switched from @variables 
//= to arguments.[kobra_k88]
//= 1.2 Rewrote inn script [Playtester]
//= 1.3 Added (finally) Rachel Inn Maid. Official warp
//=	  and save points[erKURITA]
//= 1.4 Added Hugel inn [erKurita]
//= 1.5 Added "end;" after warping the player. [L0ne_W0lf]
//=	- Small corrections to the prontera Inns.
//=	- Removed the Cancel dialog.
//= 1.6 More fixes, changed progression of "Rest"ing.
//=	- Corrected Morroc NPCs. [L0ne_W0lf]
//= 1.7 Corrected Payon NPC. [L0ne_W0lf] 
//= 1.8 Corrected Geffen NPC. [L0ne_W0lf] 
//= 1.9 Added the Inn for Al De baran. [L0ne_W0lf]
//= 2.0 Added Inn for the Town of Veins. [L0ne_W0lf]
//= 2.1 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 2.1a Corrected a little Typo error. (bugreport:536 & 537) [Samuray22]
//= 2.2 Small update to Vein's Inn Master. [L0ne_W0lf]
//= 2.3 Added Lighthalzen Inn from Lighthalzen town file. [L0ne_W0lf]
//= 2.4 Fixed Inn Employee#Ahlma warping to same destination as save point, bugreport: 191.
//= 2.5 Added Moskovia Gostinitsa (Inn) from jA
//= 2.6 Updated Alberta inn to official. [L0ne_W0lf]
//= 2.7 Removed Morroc Inns as per episode 12.1. [L0ne_W0lf]
//= 2.7a Traduzido e Otimizado. [RoM]
//= 2.8 Removido Moscóvia; faz parte das quests. [RoM]
//=========================================================

//======================== Prontera ===================================
// Estalagem do Oeste -------------------
prt_in,244,135,2	script	Estalajadeira#Sammy	53,{

	callfunc "F_InnMaid","[Funcionária Sammy]","Estalagem do Oeste","prt_in",238,130;
	warp "prt_in",247,104; end;
}
// Hospedaria do Leste -------------------
prt_in,61,141,2	script	Estalajadeira#Ahlma	53,{

	callfunc "F_InnMaid","[Funcionária Ahlma]","Estalagem do Leste","prt_in",64,136;
	warp "prt_in",60,166; end;
}


//======================== Alberta ====================================
alberta_in,32,142,3	script	Estalajadeira#Jennie	53,{

	callfunc "F_InnMaid","[Funcionária Jennie]","'Estalagem do Pescador'!","alberta_in",26,142;
	warp "alberta_in",18,188; end;
}

//====================== Geffen ======================================
geffen_in,70,64,5	script	Estalajadeira#Cena	53,{

	callfunc "F_InnMaid","[Funcionária Cena]","'Ifrit,' a única estalagem na cidade de Geffen.","geffen_in",70,59;
	warp "geffen_in",31,31; end;
}


//======================= Payon ======================================
payon_in01,132,62,5	script	Estalajadeira#Ahee	53,{

	callfunc "F_InnMaid","[Funcionária Ahee]","Estalagem de Payon","payon_in01",136,61;
	warp "payon_in01",132,11; end;
}

//======================== Morocc ====================================
// Hospedaria do Nordeste ----------------------
/*
morocc_in,147,141,3	script	Estalajadeira#Hasna	53,{

	callfunc "F_InnMaid","[Funcionária Hasna]","Estalagem de Morroc","morocc_in",142,140;
	warp "morocc_in",173,135; end;
}
// Hospedaria do Sul ---------------------------
morocc_in,80,100,5	script	Estalajadeira#Manar	53,{

	callfunc "F_InnMaid","[Funcionária Manar]","Estalagem de Morroc","morocc_in",78,95;
	warp "morocc_in",79,123; end;
}
*/

//====================== Aldebaran ===================================
aldeba_in,92,58,5	script	Estalajadeira#Rilim	53,{

	callfunc "F_InnMaid","[Rilim]","Estalagem de Al De Baran","aldeba_in",92,50;
	warp "aldeba_in",92,112; end;
}

//======================== Rachel ====================================
ra_in01,376,69,4	script	Estalajadeira#Annie	931,{

	callfunc "F_InnMaid","[Annie]","Estalagem de Rachel","ra_in01",375,58;
	warp "ra_in01",384,128; end;
}

//====================== Lighthalzen =================================
lhz_in02,230,284,4	script	Funcionário do Hotel#01	86,{

	show "[Funcionário do Hotel]";
	show "Bem-vindo ao";
	show "Royal Dragon,";
	show "onde você vai encontrar";
	show "as melhores acomodações";
	show "e os melhores serviços.";
	next;
	switch(select("Salvar","Descansar - 5,000 zeny","Cancelar")) {
	case 1:
		savepoint "lhz_in02",209,275;
		show "[Funcionário do Hotel]";
		show "Muito obrigado, seu";
		show "Ponto de Retorno foi";
		show "salvo aqui, no";
		show "Royal Dragon.";
		close;
	case 2:
		if(Zeny < 5000) {
			show "[Funcionário do Hotel]";
			show "Desculpe-me, mas você parece";
			show "não ter zeny o suficiente. Por favor";
			show "volte quando tiver!";
			close;
		}
		set Zeny,Zeny - 5000;
		show "[Funcionário do Hotel]";
		show "Muito obrigado~";
		show "Espero que você aproveite";
		show "sua estadia aqui";
		show "no Royal Dragon.";
		close2;
		warp "lhz_in02",219,150;
		percentheal 100,100;
		sc_start SC_BLESSING,240000,10;
		skilleffect AL_BLESSING,0;
		end;
	case 3:
		show "[Funcionário do Hotel]";
		show "Muito obrigado e";
		show "tenha um ótimo dia.";
		close;
	}
}

//======================== Hugel ====================================
hu_in01,246,107,3	script	Estalajadeira#hug	53,{

	callfunc "F_InnMaid","[Recepcionista]","Estalagem de Hugel","hu_in01",263,95;
	warp "hu_in01",267,5; end;
}

//======================== Veins ====================================
ve_in,157,219,5	script	Chefe da Estalagem#vei	709,{
	show "[Chefe da Estalagem]";
	show "Bom dia~";
	show "Bem-vindo à mais";
	show "confortável Estalagem";
	show "aqui em Veins~";
	next;
	switch(select("Salvar","Descansar -> 5000 zeny","Cancelar")) {
	case 1:
		show "[Chefe da Estalagem]";
		show "Seu Ponto de Retorno";
		show "foi salvo em Veins.";
		show "Curta sua estadia na cidade~";
		savepoint "ve_in",157,209;
		close;
	case 2:
		show "[Chefe da Estalagem]";
		if(Zeny < 5000){
			show "Desculpe-me, mas você não tem a quantidade necessária de zeny para fazer o check in.";
			show "O serviço custa 5000 zeny.";
			close;
		}
		show "Curta sua estadia~";
		close2;
		set Zeny,Zeny - 5000;
		percentheal 100,100;
		warp "ve_in",184,228;
	case 3:
		close;
	}
}

//======================== Brasilis ====================================
bra_in01,27,24,3	script	Recepcionista#bra	478,{
	mes "[Recepcionista]";
	mes "Bem-vindo à Pousada Tucano Feliz,";
	mes "temos tudo para fazer você se";
	mes "sentir em casa.";
	next;
	switch(select("Salvar:Descansar -> 1000 zeny:Cancelar")) 
	{
		case 1:
			mes "[Recepcionista]";
			mes "Quer salvar seu ponto de retorno aqui na Pousada?";
			next;
			if (select("Agora não.","Com certeza!") == 1)
			{
				mes "Então, tudo bem. Volte se mudar de idéia.";
				close;
			}
			mes "Está salvo. Aproveite bem sua";
			mes "estadia em Brasilis e não deixe de";
			mes "conferir nossas belezas naturais.";
			savepoint "bra_in01",143,68;
			close;
		case 2:
			mes "[Recepcionista]";
			if(Zeny < 1000){
				mes "A taxa de serviço é de 1000 zenys.";
				close;
			}
			mes "Desfrute da sua estadia!";
			close2;
			set Zeny,Zeny - 1000;
			percentheal 100,100;
			warp "bra_in01",144,69;
			end;
		case 3:
			close;
	}
}

//----------------------- Funções da Hospedagem ---------------------
function	script	F_InnMaid	{
	show getarg(0);
	show "Bem-vindo à";
	show getarg(1) + ".";
	show "Como posso ajudá-lo?";
	next;
	switch(select("Salvar:Descansar - 5.000z:Cancelar")){
	case 1:
		show getarg(0);
		show "Seu Ponto de Retorno foi salvo.";
		show "Obrigado e volte sempre.";
		savepoint getarg(2),getarg(3),getarg(4);
		close;
	case 2:
		show getarg(0);
		if(Zeny < 5000){
			show "A taxa de serviço é 5.000 zeny.";
			show "Por favor, tenha certeza de que tem a quantidade necessária";
			show "de zeny para o serviço.";
			close;
		}
		show "Obrigado.";
		show "Eu espero que você";
		show "aproveite sua estadia~";
		close2;
		set Zeny,Zeny - 5000;
		percentheal 100,100;
		return;
	case 3:
		close;
	}
}