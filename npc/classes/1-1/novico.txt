//===== Cronus Script ========================================
//= Quest de Classe - Noviço
//===== Por: =================================================
//= kobra_k88; L0ne_W0lf
//===== Versão Atual: ========================================
//= 2.3
//===== Compatível Com: ======================================
//= Cronus SVN
//===== Descrição: ===========================================
//= [Conversão Aegis]
//= Quest para se tornar Noviço
//===== Comentários Adicionais: ==============================
//= v1.0 Fully working.
//= v1.1 Added function calls for Priest Quest.
//= v1.2 Fixed Marthilda, Yosuke bugs. [Lupus]
//= v1.2a Added instant job change for High Novice [Lupus]
//= v1.3 Added Baby Class support [Lupus]
//= v1.4 Optimised and moved first checker to Jfunc1-1 [massdriller]
//= 1.5 Now saves/restores all quest skills [Lupus]
//= 1.6 Merged back JFunc [Lupus]
//= 1.7 Rescripted to Aegis 10.3 stadards. [L0ne_W0lf]
//=	No longer uses function "F_ToHigh"
//=	Proper dialog for Priest job quest is unused (for now.)
//= 1.7a Functions now make use of proper priest quest dialog [L0ne_W0lf]
//= 1.8 Removed an Unnecessary "set job_acolyte_q,0;" [Samuray22]
//= 1.8a More bug fixes care of Crashy. [L0ne_W0lf]
//= 1.9 Changed Marthilda to Mathilda. [L0ne_W0llf]
//= 2.0 Fixed High Acolytesnot being given holy light. [L0ne_W0lf]
//= 2.1 Traduzido e revisado. [.zuport]
//= 2.2 Adicionados comandos do Quest Log. [LEOzinn]
//= 2.3 Fixado problema ao completar a quests [Asus]
//============================================================

prt_church,184,41,4	script	Sacerdote#aco	60,{
	if (Upper == 1) {
		if (ADVJOB == Job_High_Priest || ADVJOB == Job_Champion) {
			if (Class == Job_Novice_High) {
				show "[Bispo Tomas Cerbantes]";
				show "Ah, eu sinto que tenha sofrido";
				show "uma experiência de vida passada. Você deve ter aprendido muitas coisas antes de entrar em Valhalla.";
				next;
				if (getskilllv("NV_BASIC") < 9) {
					show "[Bispo Tomas Cerbantes]";
					show "Infelizmente, acho que você ainda não está pronto para se tornar um Noviço. Por favor termine de aprender todas Habilidades Básicas primeiro.";
					next;
					show "[Bispo Tomas Cerbantes]";
					show "Entretanto,";
					show "eu o esperarei até";
					show "que esteja pronto.";
					show "Que Deus esteja";
					show "com você.";
					close;
				}
				show "[Bispo Tomas Cerbantes]";
				show "Bem, eu te dou as boas-vindas";
				show "pela volta de Valhalla e";
				show "te desejo boa sorte na sua";
				show "nova jornada de vida.";
				next;
				skill 143,0,0;
				jobchange Job_Acolyte_High;
				skill 156,1,0;
				show "[Bispo Tomas Cerbantes]";
				show "Agora, siga em frente e procure por aqueles que precisam da sua ajuda. Que Deus ilumine seu caminho.";
				close;
			}
			else {
				show "[Bispo Tomas Cerbantes]";
				show "Agora, siga em frente e procure por aqueles que precisam da sua ajuda. Que Deus ilumine seu caminho.";
				close;
			}
		}
		else {
			show "[Bispo Tomas Cerbantes]";
			show "Eu sinto que você já teve uma experiência de vida passada. Você deve ter aprendido muitas coisas antes de entrar em Valhalla.";
			next;
			show "[Bispo Tomas Cerbantes]";
			show "Entretanto, eu posso te dizer que você não se adequa para ser um Noviço. Por favor relembre quem você foi na sua vida passada e ache seu caminho.";
			close;
		}
	}
	show "[Bispo Tomas Cerbantes]";
	show "O que você deseja?";
	next;
	switch(select("Padre, eu quero ser um Noviço.:Os requerimentos.:Apenas olhando por aí.")) {
	case 1:
		show "[Bispo Tomas Cerbantes]";
		if (BaseJob == Job_Acolyte) {
			show "Você está se sentindo bem hoje? Eu posso dizer pelo seu vestuário que você já é um Noviço. Você está apenas brincando, não está?";
			close;
		}
		else if (BaseJob != Job_Novice) {
			show "Desculpe, mas nós só podemos aceitar Aprendizes como candidatos a Noviço.";
			close;
		}
		if (job_acolyte_q == 0) {
			show "Você realmente";
			show "deseja se tornar";
			show "um servo de Deus?";
			next;
			if (select("Sim Padre, eu quero.:Não, eu menti.") == 1) {
				show "[Bispo Tomas Cerbantes]";
				show "Ótimo. Eu aceito que você, " + strcharinfo(0) + " se tornará um Noviço. Você compreende que você deverá fazer uma missão antes de se tornar um servo de Deus, certo?";
				next;
				show "[Bispo Tomas Cerbantes]";
				show "Bem, eu te direi";
				show "a missão...";
				switch(rand(3)) {
				default:
					set job_acolyte_q,2;
					show "Por favor visite ^000077Irmão Simão^000000, um membro da paróquia de Prontera e volte aqui. Ele está praticando devotismo nas ^000077Relíquias ao nordeste da Cidade de Prontera^000000.";
					setquest 1001;
					break;
				case 1:
					set job_acolyte_q,3;
					show "Por favor visite ^000077Irmã Matilda^000000 e então volte aqui. Ela está praticando devotismo próximo à ^000077Cidade de Morroc, sudoeste da Cidade de Prontera^000000.";
					setquest 1002;
					break;
				case 2:
					set job_acolyte_q,4;
					show "Por favor visite ^000077Irmão Bartolomeu^000000 e volte aqui. Ele está praticando devotismo ao redor de uma ^000077Ponte em algum lugar ao noroeste de Prontera^000000.";
					setquest 1003;
					break;
				}
				next;
				show "[Bispo Tomas Cerbantes]";
				show "Que a graça de Deus ilumine seu caminho e o guie durante sua missão.";
				close;
			}
			show "[Bispo Tomas Cerbantes]";
			show "Você mentiu?";
			show "É bom que você";
			show "tenha confessado seu";
			show "pecado. Vá em";
			show "paz, meu filho.";
			close;
		}
		show "Oh, você voltou. Deixe-me checar e ver se você está pronto para servir a Deus. Vamos ver...";
		next;
		show "[Bispo Tomas Cerbantes]";
		if (getskilllv("NV_BASIC") < 9) {
			show "Bom Deus! Você ainda não completou o Treinamento Básico?! É importante que você termine isso!";
			next;
			show "[Bispo Tomas Cerbantes]";
			show "Você deve treinar mais! Volte e tenha certeza que alcançará o Nível 9 de Classe e aprendeu todas Habilidades Básicas!";
			close;
		}
		if (job_acolyte_q < 5) {
			show "Oh? Eu não achei seu nome na lista de registros.";
			next;
			switch(job_acolyte_q) {
			case 2:
				show "[Bispo Tomas Cerbantes]";
				show "Por favor visite ^000077Irmão Simão^000000, um membro da paróquia de Prontera e volte aqui.";
				next;
				mes "[Bispo Tomas Cerbantes]";
				mes "Ele está praticando devotismo nas ^000077Relíquias ao nordeste da Cidade de Prontera^000000.";
				break;
			case 3:
				show "[Bispo Tomas Cerbantes]";
				show "Por favor visite ^000077Irmã Matilda^000000 e então volte aqui.";
				next;
				mes "[Bispo Tomas Cerbantes]";
				mes "Ela está praticando devotismo próximo à ^000077Cidade de Morroc, sudoeste da Cidade de Prontera^000000.";
				break;
			case 4:
				show "[Bispo Tomas Cerbantes]";
				show "Por favor visite ^000077Irmão Bartolomeu^000000 e volte aqui.";
				next;
				mes "[Father Mareusis]";
				mes "Ele está praticando devotismo ao redor de uma ^000077Ponte em algum lugar ao noroeste de Prontera^000000.";
				break;
			}
			next;
			show "[Bispo Tomas Cerbantes]";
			show "Que a graça de Deus ilumine seu caminho e o guie durante sua missão.";
			close;
		}
		show "Hmm...";
		show "Seu nome está na lista e você provou sua qualificação.";
		next;
		show "[Bispo Tomas Cerbantes]";
		show "Estou orgulhoso de dizer que você agora está pronto para se tornar um Noviço!";
		next;
		skill 143,0,0;
		callfunc "Job_Change",Job_Acolyte;
		callfunc "F_ClearJobVar";
		if(checkquest(1001) != -1) {
			completequest 1001;
		}
		else if(checkquest(1002) != -1) {
			completequest 1002;
		}
		else {
			completequest 1003;
		}
		show "[Bispo Tomas Cerbantes]";
		show "Sempre lembre-se de agradecer a Deus, que está cuidando de nós todo tempo.";
		next;
		show "[Bispo Tomas Cerbantes]";
		show "Sempre use seus dons para servir Ele, ajudando os outros. No caos e tempos difíceis, enfrente as dificuldades com a sua fé inabalável.";
		next;
		show "[Bispo Tomas Cerbantes]";
		show "E por último, eu quero te dar os parabéns pela sua perseverança em sua missão.";
		close;
	case 2:
		show "[Bispo Tomas Cerbantes]";
		show "Você deseja se tornar um Noviço? Você deve cumprir dois requerimentos.";
		next;
		show "[Bispo Tomas Cerbantes]";
		show "Primeiro, você deve ter alcançado o Nível 9 de Classe como Aprendiz e ter aprendido todas Habilidades Básicas. Segundo, você terá que completar uma missão que será designada a você.";
		next;
		show "[Bispo Tomas Cerbantes]";
		if (job_acolyte_q != 0) {
			switch(job_acolyte_q) {
			case 2:
				show "Por favor visite ^000077Irmão Simão^000000, um membro da paróquia de Prontera, e volte aqui.";
				next;
				mes "[Bispo Tomas Cerbantes]";
				mes "Ele está praticando devotismo nas ^000077Relíquias ao nordeste da Cidade de Prontera^000000.";
				break;
			case 3:
				show "Por favor visite ^000077Irmã Matilda^000000 e então volte aqui.";
				next;
				mes "[Bispo Tomas Cerbantes]";
				mes "Ela está praticando devotismo próximo à ^000077Cidade de Morroc, sudoeste da Cidade de Prontera^000000.";
				break;
			default:
				show "Por favor visite ^000077Irmão Bartolomeu^000000 e volte aqui.";
				next;
				mes "[Bispo Tomas Cerbantes]";
				mes "Ele está praticando devotismo ao redor de uma ^000077Ponte em algum lugar ao noroeste de Prontera^000000.";
				break;
			}
			next;
			show "[Bispo Tomas Cerbantes]";
			show "Que a graça de Deus ilumine seu caminho e o guie durante sua missão.";
		}
		else {
			show "O destinatário dessa missão será decidido assim que você preencher o formulário de candidatura.";
		}
		next;
		show "[Bispo Tomas Cerbantes]";
		show "Por favor volte depois de preencher os dois requerimentos que eu te dei. Enquanto o seu desejo de servir a Deus e aos outros é sincero, você será capaz de fazer isso.";
		close;
	case 3:
		close;
	}
}

prt_fild03,365,255,2	script	Devoto#aco	89,{
	show "[Irmão Simão]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 6) {
			show "Por favor tome cuidado. Eles já devem saber que você veio até mim até você chegar lá.";
			next;
			show "[Irmão Simão]";
			show "Eu enviei um pombo correio com uma mensagem. Eu espero que ele chegue lá em segurança...";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 2) {
				show "Oh...? Você deve ser um dos aspirantes a Noviço. Eu já recebi a mensagem do Santuário avisando que você viria.";
				next;
				show "[Irmão Simão]";
				show "Agora, seu nome é " + strcharinfo(0) + ", certo? Excelente, obrigado por visitar-me.";
				next;
				show "[Irmão Simão]";
				show "Eu acredito que você ouviu muito do Bispo Tomas Cerbantes sobre Noviços. Além disso, há muitas pessoas que gostam de ajudar no Santuário de Prontera.";
				next;
				show "[Irmão Simão]";
				show "Eu acho que não há necessidade de ensinar mais algo pra você. Além disso, eu tenho certeza que alguém da sua geração vai ter problemas escutando um velho homem como eu. Hahaha~";
				next;
				show "[Irmão Simão]";
				show "Ainda assim, os ensinamentos podem vir de lugares que você menos espera. Deus ama ensinar suas crianças de maneiras estranhas. Você verá.";
				next;
				show "[Irmão Simão]";
				show "Bem, eu enviarei uma mensagem dizendo que você veio me visitar. Então, agora você pode retornar para o Santuário de Prontera.";
				next;
				show "[Irmão Simão]";
				show "Adeus.";
				close2;
				savepoint "prt_fild03",361,255;
				set job_acolyte_q,6;
				end;
			}
			else {
				show "Oh...";
				show "Você é um dos";
				show "Candidatos a Noviço...?";
				show "Vamos ver...";
				next;
				show "[Irmão Simão]";
				show "Seu nome é " + strcharinfo(0) + "?";
				show "Eu não acho que seu nome";
				show "está na minha lista. Hmmm...";
				next;
				show "[Irmão Simão]";
				show "Por que você não volta para o Santuário de Prontera e verifica novamente?";
				close;
			}
		}
		else {
			show "Huh? O que o traz aqui? Esse é um lugar muito perigoso para um Aprendiz como você!";
			close;
		}
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_PadreRub";
	else {
		if (BaseJob == Job_Priest) {
			show "Saudações.";
			next;
			show "[Irmão Simão]";
			show "Bem-vindo ao Abismo. Sinta-se livre para sentar e contemplar a palavra de Deus comigo. Esse lugar é maravilhoso, mesmo cercado pelo perigo há uma serenidade...";
			close;
		}
		else {
			show "Oh ho...";
			show "Você veio aqui no Abismo para treinar? Ou você é apenas um Andarilho?";
			next;
			show "[Irmão Simão]";
			show "Sejá lá o que você for, por favor tome cuidado. Os monstros aqui são fortes, ao contrário de sua meiga aparência.";
			close;
		}
	}
}

moc_fild07,41,355,4	script	Devota#2aco	95,{
	show "[Irmã Matilda]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 7) {
			show "Eu enviarei um pombo correio para o Santuário de Prontera. Quando você retornar, o Sacerdote já terá recebido minha mensagem.";
			next;
			show "[Irmã Matilda]";
			show "Eu orarei a Deus e espero que você se torne um Noviço em breve.";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 3) {
				show "Ah, você deve ser um dos candidatos a Noviço. Quero te dar as boas-vindas.";
				next;
				show "[Irmã Matilda]";
				show "Qual é seu nome? " + strcharinfo(0) + "? Vamos ver... Ah, você está na minha lista.";
				next;
				show "[Irmã Matilda]";
				show "Eu enviarei uma mensagem ao Santuário comfirmando que você, " + strcharinfo(0) + " visitou-me e completou sua missão.";
				next;
				show "[Irmã Matilda]";
				show "Por favor volte para o Santuário em Prontera e fale com o Sacerdote.";
				close2;
				savepoint "moc_fild07",35,355;
				set job_acolyte_q,7;
				end;
			}
			else {
				show "Ah...!";
				show "Você deve ser um";
				show "dos candidatos a Noviço.";
				show "Quero te dar as boas-vindas.";
				next;
				show "[Irmã Matilda]";
				show "Agora, qual é o seu nome?";
				show "" + strcharinfo(0) + "? Deixe-me ver...";
				next;
				show "[Irmã Matilda]";
				show "Hmm...";
				show "Parece que seu nome";
				show "não está na minha lista...";
				next;
				show "[Irmã Matilda]";
				show "Talvez você deva voltar para o Santuário de Prontera e verificar o seu destino da sua missão santa novamente.";
				close;
			}
		}
		else {
			show "...";
			close;
		}
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_MadreMart";
	else {
		if (BaseJob == Job_Priest) {
			show "Olá~";
			next;
			show "[Irmã Matilda]";
			show "Como está indo sua prática? Eu certamente espero que você esteja aproveitando a vida com a graça de Deus.";
			close;
		}
		else {
			show "Que Deus";
			show "esteja com você...";
			close;
		}
	}
}

prt_fild00,208,218,6	script	Devoto#3aco	98,{
	show "[Irmão Bartolomeu]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 8) {
			show "O que?";
			next;
			show "[Irmão Bartolomeu]";
			show "Você tem algum negócio pra tratar comigo?! Não tem! Volte para o Santuário agora!";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 4) {
				show "Ei.";
				show "O que quer que você seja,";
				show "você parece com um";
				show "candidato a Noviço.";
				show "Certo?";
				next;
				show "[Irmão Bartolomeu]";
				show "Nada mau, nada mau. Você resistiu muito bem ao processo de castigo.";
				show "Então qual o seu nome?";
				next;
				show "[Irmão Bartolomeu]";
				show "" + strcharinfo(0) + ", huh?";
				next;
				show "[Irmão Bartolomeu]";
				show "Ok. Eu mandarei uma mensagem para o Santuário dizendo que, " + strcharinfo(0) + ", veio me visitar.";
				next;
				show "[Irmão Bartolomeu]";
				show "Agora volte para o Santuário e termine de se tornar um Noviço, criança.";
				close2;
				savepoint "prt_fild00",206,230;
				set job_acolyte_q,8;
				end;
			}
			else {
				show "Ei.";
				show "Você parece um candidato a Noviço. Não é?";
				next;
				show "[Irmão Bartolomeu]";
				show "Nada mau, você fez toda essa caminhada de Prontera até aqui. Então qual é o seu nome, criança?";
				next;
				show "[Irmão Bartolomeu]";
				show "" + strcharinfo(0) + ", huh? Por que seu nome não está na minha lista?";
				next;
				show "[Irmão Bartolomeu]";
				show "Você provavelmente cometeu um engano. Volte para o Santuário, e verifique com o Bispo.";
				close;
			}
		}
		else {
			show "Você...";
			show "Aprendiz.";
			show "Tem alguma coisa";
			show "que você quer me dizer?";
			close;
		}
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_PadreYos";
	else {
		if (BaseJob == Job_Priest) {
			show "Ei...";
			next;
			show "[Irmão Bartolomeu]";
			show "Se você gostar, venha sentar aqui comigo e meditar as grandes verdades. A majestade de Deus é verdadeiramente inspiradora...";
			close;
		}
		else {
			show "Você tem algo para falar? Porque infelizmente para você,";
			show "eu não tenho nenhuma resposta.";
			close;
		}
	}
}