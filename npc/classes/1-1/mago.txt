//===== Cronus Script ======================================== 
//= Quest do Mage Job Quest
//===== Por: =================================================
//= kobra_k88; L0ne_W0lf
//===== Versão Atual: ======================================== 
//= 1.9
//===== Compatível com: ====================================== 
//= Cronus SVN
//===== Descrição: =========================================== 
//= [Aegis Conversion]
//= Quest para Classe Mago
//===== Comentários adicionais: ==============================
//= Fully working
//= 1.1 Fixed input ingridients bug [Lupus]
//= v1.2 Added instant job change for High Novice [Lupus]
//= 1.3 Added Baby Class support [Lupus]
//= v1.4 Optimised and moved first checker to Jfunc1-1 [massdriller]
//= 1.5 Now saves/restores all quest skills [Lupus]
//= 1.6 Merged back JFunc, CHANGED NUMBERS TO CONSTANTS [Lupus]
//= 1.7 Rescripted to Aegis 10.3 stadards. [L0ne_W0lf]
//=	No longer uses function "F_ToHigh"
//= 1.7a Fixed a "If" in the quest. (bugreport:489) [Samuray22]
//= 1.7b Retraduzido [Kamper]
//= 1.7c Revisado [Asus]
//= 1.8 Adicionados comandos do Quest Log. [LEOzinn]
//= 1.9 Corrigido problema ao completar a quests [Asus]
//============================================================

geffen_in,164,124,4	script	Guilda dos Magos	123,{
	if (Upper == 1) {
		if (ADVJOB == Job_High_Wizard || ADVJOB == Job_Professor) {
			if (Class == Job_Novice_High) {
				show "[Guardiã da Guilda dos Magos]";
				show "Uau, quanto tempo não nos vemos! Mas você não devia estar morto?";
				next;
				show "[Guardiã da Guilda dos Magos]";
				show "Ah, você deve ter renascido. Bem, etou contente em tê-lo de volta.";
				next;
				if (getskilllv("NV_BASIC") < 9) {
					show "[Guardiã da Guilda dos Magos]";
					show "Sinto muito, mas não acho que você esteja pronto para aprender magia ainda. Por que você não vai terminar de aprender as Habilidades Básicas primeiro?";
					next;
					show "[Guardiã da Guilda dos Magos]";
					show "Quanto mais você aprender, mais você estará pronto para aprender magia novamente.";
					close;
				}
				show "[Guardiã da Guilda dos Magos]";
				show "Bem, já que você você passou no teste dos Magos uma vez, eu não irei questionar sua qualificação. Você quer ter suas habilidades mágicas de volta imediatamente, não quer?";
				next;
				skill 143,0,0;
				jobchange Job_Mage_High;
				skill 157,1,0;
				show "[Guardiã da Guilda dos Magos]";
				show "Uau, por alguma razão, sua parência está muito melhor do que era antes. De qualquer maneira, eu acredito que você irá fazer um trabalho melhor sendo um Mago.";
				close;
			}
			else {
				show "[Guardiã da Guilda dos Magos]";
				show "Existe algo mais em que eu possa ajudá-lo? Se não, por que não vai testar suas habilidades? O mundo está esperando por você~!";
				close;
			}
		}
		else {
			show "[Guardiã da Guilda dos Magos]";
			show "O quê, você está interessado no Clã dos Magos? Eu não queria te dizer isso, mas você não pertence à este lugar.";
			next;
			show "[Guardiã da Guilda dos Magos]";
			show "Não sei porque você ainda está de pé diante de mim, mas posso dizer que você não se tornará um Mago.";
			close;
		}
	}
	show "[Guardiã da Guilda dos Magos]";
	show "Olá, como posso ajudá-lo?";
	next;
	switch(select("Sou um apaixonado por magias, quero ser um Mago!:Fale-me os Requirimentos.:Nada demais.")) {
	case 1:
		show "[Guardiã da Guilda dos Magos]";
		if (BaseJob == Job_Mage) {
			show "Hei, e não foi realizado? Você é um Mago, bobo!";
			next;
			show "[Guardiã da Guilda dos Magos]";
			show "Em algum dia, você irá perceber o poder dentro de você quando você puder criar fogo com sua mente!";
			close;
		}
		if (BaseJob != Job_Novice) {
			show "Hei, venha cá. Você não pode se tornar um Mago porque você possui outra Classe.";
			close;
		}
		if (job_magician_q == 0) {
			show "Se tornar um Mago, é...?";
			if (sex)
				show "Hei, olhe para você! Você é muito fofo~! Embora não seja meu tipo...";
			else {
				show "Oooh, você é uma garota muito sexy~!";
				show "Eu gosto de garotas como você~";
			}
			next;
			show "[Guardiã da Guilda dos Magos]";
			show "Certo, você disse que deseja se tornar um Mago? Tudo bem então, por favor, preencha essa ficha de candidatura a Mago.";
			next;
			if (select("Preencher.:Sair.") == 1) {
				show "[Guardiã da Guilda dos Magos]";
				show "Ok. Preenchido corretamente. Oh, você é muito bom em ortografia. Certo. Então seu nome é... " + strcharinfo(0) + ".";
				next;
				show "[Guardiã da Guilda dos Magos]";
				show "Agora está na hora";
				show "de eu lhe passar o teste.";
				switch(rand(3)) {
				case 1:
					show "Faça a ^3355FFSolução Mista Nº. 1^000000";
					show "e traga-a para mim.";
					set job_magician_q,1;
					break;
				case 2:
					show "Faça a ^3355FFSolução Mista Nº. 2^000000";
					show "e traga-a para mim.";
					set job_magician_q,2;
					break;
				case 3:
					show "Faça a ^3355FFSolução Mista Nº. 3^000000";
					show "e traga-a para mim.";
					set job_magician_q,3;

					break;
				default:
					show "Faça a ^3355FFSolução Mista Nº. 4^000000";
					show "e traga-a para mim.";
					set job_magician_q,4;
				}
				next;
				getitem 1092,1; //Tubo de Ensaio
				show "[Guardiã da Guilda dos Magos]";
				show "Você pode encontrar os ingredientes necessários no Livro de Guia neste Clã. Então, é melhor que olhe o que você precisa, antes de ir.";
				next;
				show "[Guardiã da Guilda dos Magos]";
				show "Depois de coletar todos os ingredientes, use a máquina no centro da sala para misturar a solução. Boa sorte!";
				close;
			}
			show "[Guardiã da Guilda dos Magos]";
			show "O queeeeê~?! Logo após você me dizer que deseja se tornar um Mago, você muda de ideia?! Seja mais decisivo!";
			close;
		}
		show "Éé? Pronto...?";
		if (getskilllv("NV_BASIC") < 9) {
			show "Ah, que chato. Você não cumpriu os requerimentos ainda.";
			next;
			show "[Guardiã da Guilda dos Magos]";
			show "Volte e chege ao Nível de Classe 9 primeiro. Não se esqueça de que você também tem que aprender todas as Habilidades Básicas antes de voltar.";
			close;
		}
		show "" + strcharinfo(0) + ", seu teste foi...";
		if (job_magician_q == 1)
			show "Fazer a Solução Mista Nº. 1.";
		else if (job_magician_q == 2)
			show "Fazer a Solução Mista Nº. 2.";
		else if (job_magician_q == 3)
			show "Fazer a Solução Mista Nº. 3.";
		else
			show "Fazer a Solução Mista Nº. 4.";
		next;
		show "[Guardiã da Guilda dos Magos]";
		show "Certo, deixe-me";
		show "checar se você fez a";
		show "solução precisamente...";
		next;
		show "[Guardiã da Guilda dos Magos]";
		if (countitem(1071) == 0 && countitem(1085) == 0 && countitem(1086) == 0 && countitem(1087) == 0 && countitem(1090) == 0) {
			show "Hei, onde está a Solução que";
			show "eu pedi...? Eu não posso checar se você não me mostrar, certo?";
			close;
		}
		else {
			if ((job_magician_q == 1 && countitem(1071) == 0) || (job_magician_q == 2 && countitem(1085) == 0) || (job_magician_q == 3 && countitem(1086) == 0) || (job_magician_q == 4 && countitem(1087) == 0)) {
				show "Espere.";
				show "Essa não é a";
				show "Solução que eu pedi!";
				next;
				show "[Guardiã da Guilda dos Magos]";
				if (job_magician_q == 1)
					show "Eu pedi que você fizesse a Solução Mista Nº. 1 e a trouxesse para mim. Vá logo e tente novamente.";
				else if (job_magician_q == 2)
					show "Eu pedi que você fizesse a Solução Mista Nº. 2 e a trouxesse para mim. Vá logo e tente novamente.";
				else if (job_magician_q == 3)
					show "Eu pedi que você fizesse a Solução Mista Nº. 3 e a trouxesse para mim. Vá logo e tente novamente.";
				else
					show "Eu pedi que você fizesse a Solução Mista Nº. 4 e a trouxesse para mim. Vá logo e tente novamente.";

				if (countitem(1071) != 0) delitem 1071,1; //Mage_Test_1
				else if (countitem(1085) != 0) delitem 1085,1; //Mage_Test_2
				else if (countitem(1086) != 0) delitem 1086,1; //Mage_Test_3
				else if (countitem(1087) != 0) delitem 1087,1; //Mage_Test_4
				else delitem 1090,1; //Mage_Test_Etc
				close;
			}
		}
		if (countitem(1071) != 0) delitem 1071,1; //Mage_Test_1
		else if (countitem(1085) != 0) delitem 1085,1; //Mage_Test_2
		else if (countitem(1086) != 0) delitem 1086,1; //Mage_Test_3
		else if (countitem(1087) != 0) delitem 1087,1; //Mage_Test_4
		else delitem 1090,1; //Mage_Test_Etc
		show "Hmm. Vejo que você realmente deu duro. Para um iniciante, isso é realmente muito bom.";
		show "Ótimo trabalho!";
		next;
		show "[Guardiã da Guilda dos Magos]";
		show "Tudo bem! Estou contente em dizer que você passou no teste dos Magos. Vou transformá-lo agora mesmo!";
		next;
		show "[Guardiã da Guilda dos Magos]";
		show "*Ahem*";
		show "Parabéns!";
		show "Agora você é um Mago!";
		next;
		callfunc "Job_Change",Job_Mage;
		callfunc "F_ClearJobVar";
		set Zeny,Zeny+50;
		if(checkquest(1005) != -1) {
			completequest 1005;
		}
		else if(checkquest(1006) != -1) {
			completequest 1006;
		}
		else if(checkquest(1007) != -1) {
			completequest 1007;
		}
		else {
			completequest 1008;
		}
		show "[Guardiã da Guilda dos Magos]";
		show "'Bem-vindo ao meu Mundo~'";
		show "Heh heh, eu só queria dizer isso. Você sabe, é uma frase de um filme muito famoso~";
		next;
		show "[Guardiã da Guilda dos Magos]";
		show "Agora que você é um Mago assim como nós, vamos ser amigos, certo?";
		close;
	case 2:
		show "[Guardiã da Guilda dos Magos]";
		show "Quer se tornar um Mago, é?";
		if (sex)
			show "Para uma gracinha como você, explicarei muito feliz os requerimentos!";
		else
			show "Explicarei muito feliz os requerimentos, tendo em minha frente uma linda garota como você!";
		next;
		show "[Guardiã da Guilda dos Magos]";
		show "Primeiro de tudo, você deve ser um Aprendiz com Nível de Classe 10 e ter aprendido todas as Habilidades Básicas. E então, você deverá passar no teste dos Magos.";
		next;
		if (job_magician_q != 0) {
			show "[Guardiã da Guilda dos Magos]";
			show "Seu teste é";
			switch(job_magician_q) {
			case 1:
				show "fazer a";
				show "^3355FFSolução Mista Nº. 1^000000";
				show "trazê-la para mim.";
				setquest 1005;
				break;
			case 2:
				show "fazer a";
				show "^3355FFSolução Mista Nº. 2^000000";
				show "trazê-la para mim.";
				setquest 1006;
				break;
			case 3:
				show "fazer a";
				show "^3355FFSolução Mista Nº. 3^000000";
				show "trazê-la para mim.";
				setquest 1007;
				break;
			default:
				show "fazer a";
				show "^3355FFSolução Mista Nº. 4^000000";
				show "trazê-la para mim.";
				setquest 1008;
			}
			next;
			show "[Guardiã da Guilda dos Magos]";
			show "Você pode encontrar os ingredientes necessários para fazer a Solução no Livro de Guia neste Clã.";
		}
		else {
			show "[Guardiã da Guilda dos Magos]";
			show "Você será informado sobre a Solução Mista que precisa fazer após preencher o formulário.";
		}
		next;
		show "[Guardiã da Guilda dos Magos]";
		show "Deixe-me saber quando você estiver pronto para se tornar um Mago, certo?";
		close;
	case 3:
		show "[Guardiã da Guilda dos Magos]";
		show "Nada...?";
		close;
	}
}

geffen_in,164,112,4	script	Máquina de Mistura	111,{
	show "[Máquina de Mistura]";
	show "Essa máquina é propriedade do Clã dos Magos de Geffen e é usado somente para misturar soluções com efeitos mágicos.";
	next;
	if (select("Usar máquina.:Cancel.") == 1) {
		show "[Máquina de Mistura]";
		show "Escolha o";
		show "Solvente para";
		show "a Solução.";
		next;
		switch(select("Solução de Payon.:Solução de Morroc.:Sem Solvente.")) {
		case 1:
			if (countitem(1089) == 0) {
				show "[Máquina de Mistura]";
				show "Erro.";
				show "Não foi possível encontrar o item.";
				show "Por favor, cheque novamente.";
				show "Processo travado.";
				close;
			}
			set .@mixitem2,1;
			break;
		case 2:
			if (countitem(1088) == 0) {
				show "[Máquina de Mistura]";
				show "Erro.";
				show "Não foi possível encontrar o item.";
				show "Por favor, cheque novamente.";
				show "Processo travado.";
				close;
			}
			set .@mixitem2,2;
			break;
		case 3:
			set .@mixitem2,0;
			break;
		}
		while (1) {
			if (.@progress == 2) {
				show "[Máquina de Mistura]";
				if (.@mixitem1_1 != 0) show "Jellopy: " + .@mixitem1_1 + " ea.";
				if (.@mixitem1_2 != 0) show "Felpa: " + .@mixitem1_2 + " ea.";
				if (.@mixitem1_3 != 0) show "Leite: " + .@mixitem1_3 + " ea.";
				if (.@mixitem2 == 0) show "Solvente: Nenhum.";
				if (.@mixitem2 == 1) show "Solvente: Solução de Payon.";
				if (.@mixitem2 == 2) show "Solvente: Solução de Morroc.";
				next;
				show "[Máquina de Mistura]";
				show "Por favor, escolha se você deseja iniciar a mistura, ou introduzir novamente o número de itens a serem misturados.";
				next;
				switch(select("Iniciar a mistura.:Introduzir novamente o número de itens.:Resetar.")) {
				case 1:
					show "[Máquina de Mistura]";
					show "Por favor, coloque os itens no Recipiente de Mistura. Certifique se os valores dos itens estão corretos.";
					next;
					show "[Máquina de Mistura]";
					show "Você não pode ajustar ou restaurar itens quando eles são colocados no Recipiente de Mistura.";
					next;
					show "[Máquina de Mistura]";
					show "Se tudo estiver correto, pressione o botão 'Misturar' quando você estiver pronto. Senão, pressione o botão 'Cancelar'.";
					next;
					if (select("Pressionar o botão 'Misturar'.:Pressionar o botão 'Cancelar'.") == 1) {
						show "[Máquina de Mistura]";
						show "Coloque os itens no Recipiente de Mistura agora. E por favor, aguarde.";
						next;
						show "[Máquina de Mistura]";
						if (countitem(909) < .@mixitem1_1) {
							show "Quantidade de Jellopy insuficiente.";
							show "Por favor, cheque novamente.";
							show "Processo travado.";
							close;
						}
						else if (countitem(914) < .@mixitem1_2) {
							show "Quantidade de Felpa insuficiente.";
							show "Por favor, cheque novamente.";
							show "Processo travado.";
							close;
						}
						else if (countitem(519) < .@mixitem1_3) {
							show "Quantidade de Leite insuficiente.";
							show "Por favor, cheque novamente.";
							show "Processo travado.";
							close;
						}
						else if (.@mixitem2 == 1 || .@mixitem2 == 2) {
							if (countitem(1089) == 0 && countitem(1088) == 0) {
								show "Solução não encontrada";
								show "Por favor, cheque novamente.";
								show "Processo travado.";
								close;
							}
						}
						if (.@mixitem1_1 != 0) delitem 909,.@mixitem1_1; //Jellopy
						if (.@mixitem1_2 != 0) delitem 914,.@mixitem1_2; //Fluff
						if (.@mixitem1_3 != 0) delitem 519,.@mixitem1_3; //Milk
						if (.@mixitem2 == 1) delitem 1089,1; //Payon_Potion
						if (.@mixitem2 == 2) delitem 1088,1; //Morocc_Potion
						show "Os itens estão prontos.";
						show "Feche a tampa.";
						set .@progress,3;
						next;
					}
					break;
				case 2:
					set .@continue,0;
					next;
					break;
				case 3:
					set .@mixitem1_1,0;
					set .@mixitem1_2,0;
					set .@mixitem1_3,0;
					set .@progress,0;
					set .@continue,0;
					show "[Máquina de Mistura]";
					show "Resetação completa.";
					show "Iniciar novamente?";
					next;
					if (select("Sim.:Não.") == 1)
						break;
					show "[Máquina de Mistura]";
					show "Processo travado.";
					show "Obrigado.";
					close;
				}
				if (.@progress == 3) break;
			}
			else if (.@progress == 1) {
				show "[Máquina de Mistura]";
				show "Nada encontrado.";
				next;
			}
			show "[Máquina de Mistura]";
			show "Selecione os itens para a msitura.";
			while(1) {
				switch(select("Jellopy.:Felpa.:Leite.:Pronto para a mistura.")) {
				case 1:
					while(1) {
						input .@input;
						if (.@input > 10000) {
							next;
							show "[Máquina de Mistura]";
							show "Erro: Limite de item excedido. Por favor, entre com valores abaixo de 10,000.";
							next;
						}
						else {
							if (countitem(909) > 0) set .@mixitem1_1,.@mixitem1_1 + .@input;
							set .@progress,2;
							break;
						}
					}
					break;
				case 2:
					while(1) {
						input .@input;
						if (.@input > 10000) {
							next;
							show "[Máquina de Mistura]";
							show "Erro: Limite de item excedido. Por favor, entre com valores abaixo de 10,000.";
							next;
						}
						else {
							if (countitem(914) > 0) set .@mixitem1_2,.@mixitem1_2 + .@input;
							set .@progress,2;
							break;
						}
					}
					break;
				case 3:
					while(1) {
						input .@input;
						if (.@input > 10000) {
							next;
							show "[Máquina de Mistura]";
							show "Erro: Limite de item excedido. Por favor, entre com valores abaixo de 10,000.";
							next;
						}
						else {
						if (countitem(519) > 0) set .@mixitem1_3,.@mixitem1_3 + .@input;
							set .@progress,2;
							break;
						}
					}
					break;

				case 4:
					if (.@progress != 2)
						set .@progress,1;
					set .@continue,1;
					next;
				}
				if (.@continue) break;
			}
		}
		show "[Máquina de Mistura]";
		show "Por favor, entre";
		show "com o Número";
		show "Mágico.";
		next;
		while(1) {
			input .@input;
			if (.@input < 1000 || .@input > 9999) {
				show "[Máquina de Mistura]";
				if (.@input == 0) {
					show "Gostaria de pular este menu?";
					next;
					if (select("Sim.:Não.") == 1) {
						break;
					}
				}
				else {
					show "Número Mágico Inválido.";
					show "Por favor, tente novamente.";
					next;
				}
			}
			else {
				show "[Máquina de Mistura]";
				show "O Número Mágico é #" + .@input + ", correto?";
				next;
				if (select("Confirmar.:Cancelar.") == 1) {
					if (.@input == 8472)
						set .@magic_powder,1;
					else if (.@input == 3735)
						set .@magic_powder,2;
					else if (.@input == 2750)
						set .@magic_powder,3;
					else if (.@input == 5429)
						set .@magic_powder,4;
					else
						set .@magic_powder,5;
				}
				break;
			}
		}
		show "[Máquina de Mistura]";
		show "Escolha um";
		show "Catalisador.";
		next;
		switch(select("Gema Amarela.:Gema Vermelha.:Gema Azul.:Diamate.:Pular.")) {
		case 1: set .@mixitem3,1; break;
		case 2: set .@mixitem3,2; break;
		case 3: set .@mixitem3,3; break;
		case 4: set .@mixitem3,4; break;
		case 5: break;
		}
		show "[Máquina de Mistura]";
		show "Dados confirmados.";
		show "Inicializando.";
		show "Misturando processos.";
		show "Por favor, aguarde.";
		next;
		show "[Máquina de Mistura]";
		show "- Provérbio do Dia -";
		switch(rand(1,5)) {
		case 1:
			show "Olho por Olho: Quando você tirar de uma pessoa, você deve substituir ou reembolsar de alguma forma.";
			break;
		case 2:
			show "Credibility is a Man's Currency: There's a value in genuine trust that cannot be measured.";
			break;
		case 3:
			show "What Goes Around Comes Around: Ultimately, you will be treated in the way you treat others.";
			show "It means 'When you harm Another you will be harmed by him in an unavoidable situation'.";
			break;
		case 4:
			show "A good neighbor is better than a distant brother: When you need help, you can count on those close to you.";
			break;
		default:
			show "Birds of a Feather Flock Together: You can look at a person's friends as an indicator of their character.";
		}
		next;
		if (.@mixitem1_1 == 2 && .@mixitem1_2 == 3 && .@mixitem1_3 == 1 && .@mixitem2 == 1 && .@mixitem3 == 1 && .@magic_powder == 1) {
			show "[Máquina de Mistura]";
			show "Solução Nº. 1 do Teste dos Magos.";
			additem 1071,1; //Mage_Test_1
			next;
		}
		else if (.@mixitem1_1 == 3 && .@mixitem1_2 == 1 && .@mixitem1_3 == 1 && .@mixitem2 == 0 && .@mixitem3 == 2 && .@magic_powder == 2) {
			show "[Máquina de Mistura]";
			show "Solução Nº. 2 do Teste dos Magos.";
			additem 1085,1; //Mage_Test_2
			next;
		}
		else if (.@mixitem1_1 == 6 && .@mixitem1_2 == 1 && .@mixitem1_3 == 0 && .@mixitem2 == 1 && .@mixitem3 == 3 && .@magic_powder == 3) {
			show "[Máquina de Mistura]";
			show "Solução Nº. 3 do Teste dos Magos.";
			additem 1086,1; //Mage_Test_3
			next;
		}
		else if (.@mixitem1_1 == 2 && .@mixitem1_2 == 3 && .@mixitem1_3 == 0 && .@mixitem2 == 2 && .@mixitem3 == 4 && .@magic_powder == 4) {
			show "[Máquina de Mistura]";
			show "Solução Nº. 4 do Teste dos Magos.";
			additem 1087,1; //Mage_Test_4
			next;
		}
		else {
			show "[Máquina de Mistura]";
			show "Erro Imprevisto";
			show "Ocorrido.";
			additem 1090,1; //Mage_Test_Etc
			next;
		}
		show "[Máquina de Mistura]";
		show "Mistura completa.";
		show "Obrigado.";
		close;
	}
	close;
}

pay_arche,122,100,0	script	Dollshoi	88,{
	show "[Guardiã da Guilda dos Magos]";
	show "Você quer a Solução? Então me dê 50 Zeny e pelo menos 1 Tubo de Teste vazio.";
	next;
	if (select("Tudo bem, vou dar.:Nah, esqueça.") == 1) {
		show "[Guardiã da Guilda dos Magos]";
		if (zeny < 50) {
			show "Hei! Você não tem dinheiro suficiente. Como disse, são 50 Zeny.";
			close;
		}
		if (countitem(1092) == 0) {
			show "Você não pode levar a Solução sem o Tubo de Teste! Traga-me um Tubo de Teste vazio.";
			close;
		}
		delitem 1092,1; //Empty_Cylinder
		set zeny,zeny-50;
		additem 1089,1; //Payon_Potion
	}
	close;
}

moc_ruins,91,150,0	script	Ponka-Hontas	93,{
	show "[Guardiã da Guilda dos Magos]";
	show "Gostaria de uma Solução? Então por favor me dê 50 Zeny e pelo menos 1 Tubo de Teste vazio.";
	next;
	if (select("Tudo bem, vou dar.:Nah, esqueça.") == 1) {
		show "[Guardiã da Guilda dos Magos]";
		if (zeny < 50) {
			show "Sinto muito, mas você não tem dinheiro suficiente. Como disse, são 50 Zeny.";
			close;
		}
		if (countitem(1092) == 0) {
			show "Você não pode levar o líquido sem um tubo. Traga-me um Tubo de Teste na próxima vez que me ver.";
			close;
		}
		delitem 1092,1; //Empty_Cylinder
		set zeny,zeny-50;
		additem 1088,1; //Morocc_Potion
	}
	close;
}

geffen_in,177,112,4	script	Livro de Guia	111,{
	show "[Livro de Guia de Soluções]";
	show "Esse Livro de Guia é propriedade da Assossiação de Magos de Geffen. Por favor use-o com cuidado.";
	next;
	switch(select("Solução Nº. 1.:Solução Nº. 2.:Solução Nº. 3.:Solução Nº. 4.:Fechar.")) {
	case 1:
		show "[Teste dos Magos - Solução Nº. 1]";
		show "* Lista de Ingredientes *";
		show "2 Jellopy";
		show "3 Felpas";
		show "1 Leite";
		next;
		show "[Teste dos Magos - Solução Nº. 1]";
		show "* Solvente *";
		show "Solução de Payon";
		show "Onde encontrar:";
		show "Em uma pequena fonte em Payon, na Vila dos Arqueiros.";
		next;
		show "[Teste dos Magos - Solução Nº. 1]";
		show "* Número Mágico *";
		show "8472";
		next;
		show "[Teste dos Magos - Solução Nº. 1]";
		show "* Catalisador *";
		show "Gema Amarela";
		show "(Providenciado pela";
		show "Máquina de Mistura)";
		close;
	case 2:
		show "[Teste dos Magos - Solução Nº. 2]";
		show "* Lista de Ingredientes *";
		show "3 Jellopy";
		show "1 Felpa";
		show "1 Leite";
		next;
		show "[Teste dos Magos - Solução Nº. 2]";
		show "* Solvente *";
		show "Nenhum";
		next;
		show "[Teste dos Magos - Solução Nº. 2]";
		show "* Número Mágico *";
		show "3735";
		next;
		show "[Teste dos Magos - Solução Nº. 2]";
		show "* Catalisador *";
		show "Gema Vermelha";
		show "(Providenciado pela";
		show "Máquina de Mistura)";
		close;
	case 3:
		show "[Teste dos Magos - Solução Nº. 3]";
		show "* Lista de Ingredientes *";
		show "6 Jellopy";
		show "1 Felpa";
		next;
		show "[Teste dos Magos - Solução Nº. 3]";
		show "* Solvente *";
		show "Solução de Payon";
		show "Onde encontrar:";
		show "Em uma pequena fonte em Payon, na Vila dos Arqueiros.";
		next;
		show "[Teste dos Magos - Solução Nº. 3]";
		show "* Número Mágico *";
		show "2750";
		next;
		show "[Teste dos Magos - Solução Nº. 3]";
		show "* Catalisador *";
		show "Gema Azul";
		show "(Providenciado pela";
		show "Máquina de Mistura)";
		close;
	case 4:
		show "[Teste dos Magos - Solução Nº. 4]";
		show "* Lista de Ingredientes *";
		show "2 Jellopy";
		show "3 Felpas";
		next;
		show "[Teste dos Magos - Solução Nº. 4]";
		show "* Solvente *";
		show "Solução de Morroc";
		show "Onde encontrar:";
		show "Em uma pequena fonte perto da entrada da pirâmide em Morroc.";
		next;
		show "[Teste dos Magos - Solução Nº. 4]";
		show "* Número Mágico *";
		show "5429";
		next;
		show "[Teste dos Magos - Solução Nº. 4]";
		show "* Catalisador *";
		show "1 Diamante";
		show "(Providenciado pela";
		show "Máquina de Mistura)";
		close;
	}
close;
}
