//===== Cronus Script ========================================
//= Guia da Cidade de Morroc
//===== Por: =================================================
//= kobra_k88; L0ne_W0lf
//===== Versão Atual: ========================================
//= 1.4c
//===== Compatível com: ======================================
//= Cronus SVN
//===== Comentários Adicionais: ==============================
//= Fully working.  Added a guide at every exit.
//= v1.1 Now using duplicate command.
//= 1.2 Optimized, updated common guide names [Lupus]
//= 1.3 Removed Duplicates [Silent]
//= 1.4 rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.4a Fixed some errors with the loop[Samuray22]
//	-Thanks to $ephiroth.
//= 1.4b Atualiado e revisado para versão mais recente [Asus]
//= 1.4c Revisado. [RoM]
//============================================================

morocc,153,286,6	script	Guia#moc::MocGuide	707,{
	cutin "moc_soldier",2;
	show "[Guia de Morroc]";
	show "Bem-vindo a Morroc,";
	show "a cidade do deserto.";
	show "Por favor me peça ajuda se";
	show "se você tiver problema em";
	show "encontrar alguma coisa na cidade.";
	set .@loop1,1;
	while (.@loop1) {
		next;
		switch(select("Indicações:Limpar Indicações:Tutorial:Cancelar")) {
		case 1:
			show "[Guia de Morroc]";
			show "Por favor selecione";
			show "uma localização no";
			show "menu a seguir.";
			if (.@compass_check == 0) {
				show "Você quer que eu";
				show "deixe indicações";
				show "no seu Mini-Mapa?";
				next;
				if (select("Sim:Não") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while(.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;

				switch(select("^FF0000Guilda dos Gatunos^000000:Loja de Armas:Estalagem:Bar:Guilda dos Mercenários:Casa de Forja:Cancelar")) {
				case 1:
					show "[Guia de Morroc]";
					show "A Guilda dos Gatunos é";
					show "o lugar onde Aprendizes";
					show "podem se tornar Gatunos,";
					show "se encontra na Pirâmide";
					show "da saída Noroeste da cidade.";
					if (.@compass_check)
						viewpoint 1,24,297,2,0xFF0000;
					break;
				case 2:
					show "[Guia de Morroc]";
					show "A Loja de Armas";
					show "se encontra na ponta";
					show "sudeste da cidade.";
					if (.@compass_check)
						viewpoint 1,253,56,3,0xFF00FF;
					break;
				case 3:
					show "[Guia de Morroc]";
					show "As Hospedarias se";
					show "encontram ao Sul e";
					show "ao nordeste da cidade.";
					if (.@compass_check) {
						viewpoint 1,197,66,4,0xFF00FF;
						viewpoint 1,273,269,5,0xFF00FF;
					}
					break;
				case 4:
					show "[Guia de Morroc]";
					show "O Bar se encontra";
					show "na parte noroeste da cidade.";
					if (.@compass_check)
						viewpoint 1,52,259,6,0xFF00FF;
					break;
				case 5:
					show "[Guia de Morroc]";
					show "A Guilda de Mercenários";
					show "se encontra na ponta";
					show "leste da cidade.";
					if (.@compass_check)
						viewpoint 1,284,171,7,0x00FF00;
					break;
				case 6:
					show "[Guia de Morroc]";
					show "A Casa de Forja";
					show "se encontra ao";
					show "sudoeste do centro";
					show "da cidade.";
					if (.@compass_check)
						viewpoint 1,47,47,7,0xFF00FF;
					break;
				case 7:
					show "[Guia de Morroc]";
					show "Se você deseja apagar as indicações do seu Mini-Mapa";
					show "por favor selecione 'Limpar Indicações' no menu.";
					set .@loop2,0;
				}
			}
			break;
		case 2:
			viewpoint 2,237,41,2,0x00FF00;
			viewpoint 2,237,41,3,0x0000FF;
			viewpoint 2,46,345,4,0x00FF00;
			viewpoint 2,175,220,5,0xFF0000;
			viewpoint 2,175,220,6,0xFF0000;
			viewpoint 2,175,220,7,0xFF0000;
			set .@compass_check,0;
			break;
		case 3:
			show "[Guia de Morroc]";
			show "Nós fomos exaustivamente treinados";
			show "para servi-lo da melhor maneira";
			show "possível e sem falhas.";
			show "Assim você tem a garantia das";
			show "melhores indicações possíveis.";
			next;
			show "[Guia de Morroc]";
			show "As indicações que lhe daremos podem";
			show "ser marcados no seu mini-mapa.";
			show "Para visualizar o seu mini-mapa, aperte 'Ctrl + Tab', ou clique no";
			show "botão 'Mapa' no seu menu principal.";
			show "Controle o seu 'zoom' no mini-mapa por meio dos símbolos '+' e '-'.";
			break;
		case 4:
			show "[Guia de Morroc]";
			show "Espero que você tenha uma jornada";
			show "segura e aproveite sua estadia";
			close2;
			set .@loop1,0;
		}
	}
	cutin "moc_soldier",255;
	end;
}

morocc,54,97,0	duplicate(MocGuide)	Guia#2moc	707